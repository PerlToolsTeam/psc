#!/usr/bin/perl

use strict;
use warnings;
use 5.34.0;

use Web::Query;
use Time::Piece;
use JSON;

my $base_url  = 'https://www.nntp.perl.org';
my $group_url = "$base_url/group/perl.perl5.porters";

my $today = localtime;
my ($this_m, $this_y) = (sprintf('%02d', $today->mon), $today->year);

my @meetings;

for my $y (2021 .. $this_y) {
  for my $m ('01' .. $this_m) {
    my $wq = wq("$group_url/$y/$m.html");
    $wq->find('table.article_list a')->each(\&process_node);

    last if $y == $this_y and $m == $this_m;
  }
}

@meetings = sort { $a->{num} <=> $b->{num} } @meetings;

my $json = JSON->new->canonical->pretty;

open my $fh, '>', 'psc.json' or die $!;

print $fh $json->encode(\@meetings);

sub process_node {
  return unless $_->text =~ /^(?:PSC|(?:Perl )?Steering Co)/i;

  my ($num)  = $_->text =~ /[ #](\d{2,3})\b/;
  my ($date) = $_->text =~ /(\d\d\d\d-\d\d-\d\d)/;
  print $_->text, "\n";
  printf "%s : %s\n", ($num // '???'), ($date // '???');

  push @meetings, {
    text => $_->text,
    num  => sprintf('%03s', $num),
    date => $date,
    href => $base_url . $_->attr('href'),
  };
}
